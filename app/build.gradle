import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.johnrengelman.shadow'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.zestworks.shadowapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// ...

configurations {
    internalize // just define a separate configuration
}

/* a task to create the relocated libs, must be defined before used below in dependencies */
task internalizeJar (type: ShadowJar) {
    classifier = 'intern' // this is not needed but prevents conflicts with other artifacts you may want to create
    def pkg = 'com.squareup.tape2' // lib to relocate
    def shadowPrefix = 'our.package.name'
    relocate pkg, [shadowPrefix, pkg].join ('.') // we want to relocate the above package
    configurations = [project.configurations.internalize] // our configuration from above
}

build.dependsOn internalizeJar

dependencies {
    shadow localGroovy()
    shadow gradleApi()

    internalize 'com.squareup.tape2:tape:2.0.0-beta1' // this lib is a dependency of our separate configuration
    implementation tasks.internalizeJar.outputs.files // our real project uses the relocated jar as dependency

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}